plugins {
    id "com.jfrog.bintray" version "1.8.4"
    id "com.github.jruby-gradle.base" version "1.6.0"
    id "java"
    id "scala"
}
import com.github.jrubygradle.JRubyExec
repositories {
    mavenCentral()
    jcenter()
}
configurations {
    provided
}

version = "0.2.1.pre"
ext {
    embulkVersion = "0.9.12"
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

dependencies {
    compile "com.ximpleware:vtd-xml:2.13.4"
    compile  "org.embulk:embulk-core:${embulkVersion}"
    provided "org.embulk:embulk-core:${embulkVersion}"
    testCompile "org.embulk:embulk-core:${embulkVersion}:tests"
    testCompile "org.embulk:embulk-standards:${embulkVersion}"
    testCompile "junit:junit:4.12"

    compile group: 'org.scala-lang', name: 'scala-library', version: '2.12.8'
    testCompile group: 'org.scalatest', name: 'scalatest_2.12', version: '3.0.5'

}

task classpath(type: Copy, dependsOn: ["jar"]) {
    doFirst { file("classpath").deleteDir() }
    from (configurations.runtime - configurations.provided + files(jar.archivePath))
    into "classpath"
}
clean { delete "classpath" }

test {
    testLogging {
        events 'failed'
        exceptionFormat 'full'
    }
}

task gem(type: JRubyExec, dependsOn: ["gemspec", "classpath"]) {
    jrubyArgs "-S"
    script "gem"
    scriptArgs "build", "${project.name}.gemspec"
    doLast {
        ant.move(file: "${project.name}-${project.version}.gem", todir: "pkg")
    }
}

task gemPush(type: JRubyExec, dependsOn: ["gem"]) {
    jrubyArgs "-S"
    script "gem"
    scriptArgs "push", "pkg/${project.name}-${project.version}.gem"
}

task "package"(dependsOn: ["gemspec", "classpath"]) {
    doLast {
        println "> Build succeeded."
        println "> You can run embulk with '-L ${file(".").absolutePath}' argument."
    }
}

task gemspec {
    ext.gemspecFile = file("${project.name}.gemspec")
    inputs.file "build.gradle"
    outputs.file gemspecFile
    doLast { gemspecFile.write($/
Gem::Specification.new do |spec|
  spec.name          = "${project.name}"
  spec.version       = "${project.version}"
  spec.authors       = ["maji-KY"]
  spec.summary       = %[Embulk parser plugin for XML]
  spec.description   = %[Parses XML files read by other file input plugins.]
  spec.email         = ["maji-KY@neco-labo.com"]
  spec.licenses      = ["GPL-2.0"]
  spec.homepage      = "https://github.com/maji-KY/embulk-parser-xpath2"

  spec.files         = `git ls-files`.split("\n") + Dir["classpath/*.jar"]
  spec.test_files    = spec.files.grep(%r"^(test|spec)/")
  spec.require_paths = ["lib"]

  #spec.add_dependency 'YOUR_GEM_DEPENDENCY', ['~> YOUR_GEM_DEPENDENCY_VERSION']
  spec.add_development_dependency 'bundler', ['~> 1.0']
  spec.add_development_dependency 'rake', ['>= 10.0']
end
/$)
    }
}
clean { delete "${project.name}.gemspec" }
